<html>

<head>
  <title>Planetarium</title>
  
  <script id="shader-vs-passthrough" type="x-shader/x-vertex">
#line 8
attribute vec3 a_Position;
uniform mat4 u_PMatrix;
uniform mat4 u_MVMatrix;
void main(void) {
  gl_Position = u_PMatrix * u_MVMatrix * vec4(a_Position, 1.0);
}
</script>
  <script id="shader-vs-tex-norm" type="x-shader/x-vertex">
#line 17
attribute vec3 a_Position;
attribute vec3 a_Normal;
attribute vec2 a_TexCoord;
uniform mat4 u_PMatrix;
uniform mat4 u_MVMatrix;
uniform mat3 u_NMatrix;
varying vec3 v_Position;
varying vec3 v_Normal;
varying vec2 v_TexCoord;
void main() {
  gl_Position = u_PMatrix * u_MVMatrix * vec4(a_Position, 1.0);
  v_Position = (u_MVMatrix * vec4(a_Position, 1.0)).xyz;
  v_Normal = u_NMatrix * a_Normal;
  v_TexCoord = a_TexCoord;
}
</script>

  <script id="shader-fs-sun" type="x-shader/x-fragment">
#line 36
precision mediump float;

varying vec2 v_TexCoord;

uniform bool u_UseColorMap;
uniform sampler2D u_ColorSampler;

const vec4 fallback = vec4(1.0, 0.0, 1.0, 1.0);

void main(void) {
  if (u_UseColorMap) {
    gl_FragColor = texture2D(u_ColorSampler, v_TexCoord);
  } else {
    gl_FragColor = fallback;
  }
}
</script>
  <script id="shader-fs-earth" type="x-shader/x-fragment">
#line 57
precision mediump float;

varying vec3 v_Position;
varying vec3 v_Normal;
varying vec2 v_TexCoord;

uniform vec3 u_PlanetPosition;
uniform vec3 u_PlanetUp;

uniform bool u_UseDayColorMap;
uniform bool u_UseNightColorMap;
uniform bool u_UseNormalMap;
uniform bool u_UseSpecularMap;

uniform sampler2D u_DayColorSampler;
uniform sampler2D u_NightColorSampler;
uniform sampler2D u_NormalSampler;
uniform sampler2D u_SpecularSampler;

uniform vec3 u_LightPosition;
uniform vec3 u_EyePosition;

const vec3 luminosityWeights = vec3(0.2126, 0.7152, 0.0722);
const vec4 fallback = vec4(1.0, 0.0, 1.0, 1.0);

const vec3 black = vec3(0.0, 0.0, 0.0);
const vec3 white = vec3(1.0, 1.0, 1.0);

void main(void) {
  vec4 dayColor;
  vec4 nightColor;
  vec3 normalOffset;
  float specularFactor;
  
  // texture samples
  if (u_UseDayColorMap) dayColor = texture2D(u_DayColorSampler, v_TexCoord);
  else dayColor = fallback;
  if (u_UseNightColorMap) nightColor = texture2D(u_NightColorSampler, v_TexCoord);
  else nightColor = dayColor * 0.2;
  if (u_UseNormalMap) normalOffset = texture2D(u_NormalSampler, v_TexCoord).rgb * 2.0 - 1.0;
  else normalOffset = vec3(0.0, 0.0, 1.0);
  if (u_UseSpecularMap) specularFactor = texture2D(u_SpecularSampler, v_TexCoord).r;
  else specularFactor = 1.0;
  
  // normal
  vec3 normal = normalize(v_Normal);
  vec3 tangent = normalize(cross(normal, u_PlanetUp));
  vec3 bitangent = normalize(cross(normal, tangent));
  normal = normalize(-normalOffset.x * tangent + -normalOffset.y * bitangent + normalOffset.z * normal);
  
  // lights
  vec3 diffuseLightNormal = normalize(u_LightPosition - u_PlanetPosition);
  vec3 specularLightNormal = normalize(u_LightPosition - v_Position);
  vec3 eyeNormal = normalize(u_EyePosition - v_Position);
  vec3 ambient = vec3(0.1, 0.1, 0.1);
  float diffuse = max(0.0, dot(normal, diffuseLightNormal));
  float specular = max(0.0, dot(normal, normalize(specularLightNormal + eyeNormal)));
  vec3 light = ambient + diffuse + pow(specular * specularFactor, 128.0);
  
  if (dot(luminosityWeights, light) <= 0.15) {
    dayColor = nightColor;
	light = vec3(1.0, 1.0, 1.0);
  }
  
  // color
  gl_FragColor = vec4(
    dayColor.rgb * light,
    dayColor.a
  );
}
</script>
  <script id="shader-fs-terrestial" type="x-shader/x-fragment">
#line 125
precision mediump float;

varying vec3 v_Position;
varying vec3 v_Normal;
varying vec2 v_TexCoord;

uniform vec3 u_PlanetPosition;
uniform vec3 u_PlanetUp;

uniform bool u_UseColorMap;
uniform bool u_UseNormalMap;

uniform sampler2D u_ColorSampler;
uniform sampler2D u_NormalSampler;

uniform vec3 u_LightPosition;

const vec3 luminosityWeights = vec3(0.2126, 0.7152, 0.0722);
const vec4 fallback = vec4(1.0, 0.0, 1.0, 1.0);

const vec3 black = vec3(0.0, 0.0, 0.0);
const vec3 white = vec3(1.0, 1.0, 1.0);

void main(void) {
  vec4 color;
  vec3 normalOffset;
  
  // texture samples
  if (u_UseColorMap) color = texture2D(u_ColorSampler, v_TexCoord);
  else color = fallback;
  if (u_UseNormalMap) normalOffset = texture2D(u_NormalSampler, v_TexCoord).rgb * 2.0 - 1.0;
  else normalOffset = vec3(0.0, 0.0, 1.0);
  
  // normal
  vec3 normal = normalize(v_Normal);
  vec3 tangent = normalize(cross(normal, u_PlanetUp));
  vec3 bitangent = normalize(cross(normal, tangent));
  normal = normalize(-normalOffset.x * tangent + -normalOffset.y * bitangent + normalOffset.z * normal);
  
  // lights
  vec3 lightNormal = normalize(u_LightPosition - u_PlanetPosition);
  vec3 ambient = vec3(0.1, 0.1, 0.1);
  float diffuse = max(0.0, dot(normal, lightNormal));
  vec3 light = ambient + diffuse;
  
  // color
  gl_FragColor = vec4(
    color.rgb * light,
    color.a
  );
}
</script>
  <script id="shader-fs-gas" type="x-shader/x-fragment">
#line 179
precision mediump float;

varying vec3 v_Position;
varying vec3 v_Normal;
varying vec2 v_TexCoord;

uniform vec3 u_PlanetPosition;

uniform bool u_UseColorMap;

uniform sampler2D u_ColorSampler;

uniform vec3 u_LightPosition;

const vec3 luminosityWeights = vec3(0.2126, 0.7152, 0.0722);
const vec4 fallback = vec4(1.0, 0.0, 1.0, 1.0);

const vec3 black = vec3(0.0, 0.0, 0.0);
const vec3 white = vec3(1.0, 1.0, 1.0);

void main(void) {
  vec4 color;
  vec3 normalOffset;
  
  // texture samples
  if (u_UseColorMap) color = texture2D(u_ColorSampler, v_TexCoord);
  else color = fallback;
  
  // normal
  vec3 normal = normalize(v_Normal);
  
  // lights
  vec3 lightNormal = normalize(u_LightPosition - u_PlanetPosition);
  vec3 ambient = vec3(0.1, 0.1, 0.1);
  float diffuse = max(0.0, dot(normal, lightNormal));
  vec3 light = ambient + diffuse;
  
  // color
  gl_FragColor = vec4(
    color.rgb * light,
    color.a
  );
}
</script>
  <script id="shader-fs-occlusion" type="x-shader/x-fragment">
#line 225
precision mediump float;
uniform vec4 u_Color;
void main() { gl_FragColor = u_Color; }
</script>
  
  <script id="shader-pp-radialBlur" type="x-shader/x-fragment">
#line 232
// source: http://evanw.github.io/glfx.js/demo/
// name: zoomBlur shader
precision mediump float;

varying vec2 v_TexCoord;

uniform sampler2D u_Sampler;
uniform vec2 u_Center;
//uniform float u_Strength;
uniform vec2 u_TextureSize;

float random(vec3 scale, float seed) {
  // use the fragment position for a different seed per-pixel
  return fract(
    sin(
      dot(gl_FragCoord.xyz + seed, scale)
    ) * 43758.5453 + seed
  );
}

void main() {
  vec4 color = vec4(0.0);
  float total = 0.0;
  vec2 toCenter = u_Center - v_TexCoord * u_TextureSize;
  
  // randomize the lookup values to hide the fixed number of samples
  float offset = random(
    vec3(12.9898, 78.233, 151.7182),
    0.0
  );
  
  const float samples = 50.0;
  for (float t = 0.0; t <= samples; t += 1.0) {
    float percent = (t + offset) / samples;
    float weight = (samples / 10.0) * (percent - percent * percent);
    vec4 sample = texture2D(
      u_Sampler,
      v_TexCoord + toCenter * percent * 0.85 / u_TextureSize
    );
    
    // switch to pre-multiplied alpha to correctly blur transparent images
    sample.rgb *= sample.a;
    
    color += sample * weight;
    total += weight;
  }
  gl_FragColor = color / total;
  
  // switch back from pre-multiplied alpha
  gl_FragColor.rgb /= gl_FragColor.a + 0.00001;
}
</script>
  <script id="shader-pp-smoothBlur" type="x-shader/x-fragment">
#line 172
precision mediump float;

uniform sampler2D uSampler;
uniform float uStrength;
uniform vec2 uTextureSize;

varying vec2 vTexCoord;

float random(vec3 scale, float seed) {
  // use the fragment position for a different seed per-pixel
  return fract(
    sin(
      dot(gl_FragCoord.xyz + seed, scale)
    ) * 43758.5453 + seed
  );
}

void main() {
  vec4 color = vec4(0.0);
  float total = 0.0;
  
  // randomize the lookup values to hide the fixed number of samples
  float offset = random(vec3(12.9898, 78.233, 151.7182), 0.0);
  
  const float samples = 100.0;
  for (float t = 0.0; t <= samples; t += 1.0) {
    float percent = (t + offset) / samples;
    float weight = (samples / 10.0) * (percent - percent * percent);
    vec4 sample = texture2D(
      uSampler,
      vTexCoord + toCenter * uStrength * percent / uTextureSize
    );
    
    // switch to pre-multiplied alpha to correctly blur transparent images
    sample.rgb *= sample.a;
    
    color += sample * weight;
    total += weight;
  }
  gl_FragColor = color / total;
  
  // switch back from pre-multiplied alpha
  gl_FragColor.rgb /= gl_FragColor.a + 0.00001;
}
</script>

  <script id="shader-vs-skybox" type="x-shader/x-vertex">
#line 339
attribute vec3 a_Position;
attribute vec2 a_TexCoord;
uniform mat4 u_PMatrix;
uniform mat4 u_MVMatrix;
varying vec2 v_TexCoord;
void main() {
  gl_Position = u_PMatrix * u_MVMatrix * vec4(a_Position, 1.0);
  v_TexCoord = a_TexCoord;
}
</script>
  <script id="shader-fs-skybox" type="x-shader/x-fragment">
#line 357
precision mediump float;
uniform bool u_Loaded;
uniform sampler2D u_Sampler;
varying vec2 v_TexCoord;

void main() {
  gl_FragColor = u_Loaded ? texture2D(u_Sampler, v_TexCoord) : vec4(0.0, 0.0, 0.0, 1.0);
}
</script>
  <script id="shader-vs-sun" type="x-shader/x-vertex">
#line 17
attribute vec3 a_Position;
attribute vec2 a_TexCoord;
uniform mat4 u_PMatrix;
uniform mat4 u_MVMatrix;
uniform mat3 u_NMatrix;
varying vec2 v_TexCoord;
void main() {
  gl_Position = u_PMatrix * u_MVMatrix * vec4(a_Position, 1.0);
  v_TexCoord = a_TexCoord;
}
</script>
</head>

<body onload="PlanetariumStart()">

<canvas id="planetarium-3dcanvas" style="border: none;" width="1250" height="500"></canvas>
<div id="debug"></div>

  <!-- libraries -->
  <script type="text/javascript" src="jquery-2.1.1.min.js"></script>
  <script type="text/javascript" src="gl-matrix-2.2.1.min.js"></script>
  <!-- planetarium scripts -->
  <script type="text/javascript" src="createSphere.js"></script>
  <script type="text/javascript" src="ycl.js"></script>
  <script type="text/javascript" src="ycl.WebGL.js"></script>
  <script type="text/javascript" src="planetarium.js"></script>
  <script type="text/javascript">
function PlanetariumStart() {
  const scene = WebGLStart('planetarium-3dcanvas')
  const debug = document.getElementById('debug')
  const mv = mat4.create()
  
  var lastDelta = 0
  var deltas = []
  var lastTime = Date.now()
  tick()
  
  function tick() {
    requestAnimFrame(tick)
    const timeNow = Date.now()
    const delta = timeNow - lastTime
    scene.draw(timeNow, delta, mv)
    lastTime = timeNow
    deltas[lastDelta++] = delta
    if (lastDelta >= 50) lastDelta = 0
    const average = deltas.reduce(function(sum, next) { return sum + next }, 0) / deltas.length
    debug.innerHTML =
      "FPS: " +
      (Math.round((10 * 1000) / average) / 10)
  }
}
</script>

</body>
</html>
